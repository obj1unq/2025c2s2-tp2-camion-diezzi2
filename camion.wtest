import caminosYAlmacen.*
import cosas.*
import camion.*

describe "Pruebas de camión, cargas, pesos, peligrosidad y accidentes" {
  test "Prueba de carga y descarga" {
    camion.cargar(bumblebee)

    assert.equals(#{bumblebee}, camion.cosas())

    camion.cargar(knightRider)

    assert.equals(#{bumblebee, knightRider}, camion.cosas())

    camion.descargar(bumblebee)

    assert.equals(#{knightRider}, camion.cosas())

    assert.throwsException({ camion.cargar(knightRider) })

    assert.throwsException({ camion.descargar(bumblebee) })
  }

  test "Comprobación de peso de cargas par e impar" {
    camion.cargar(bumblebee)
    arenaAGranel.peso(50)
    camion.cargar(arenaAGranel)
    
    assert.that(camion.todoPesoEsPar())

    arenaAGranel.peso(51)

    assert.notThat(camion.todoPesoEsPar())
  }

  test "Comprobación de peso de cargas" {
    camion.cargar(bumblebee)
    arenaAGranel.peso(50)
    camion.cargar(arenaAGranel)

    assert.that(camion.hayAlgoDePeso(50))
    assert.that(camion.hayAlgoDePeso(800))
    assert.notThat(camion.hayAlgoDePeso(500))
  }

  test "Prueba de peso total y exceso de peso de camión" {
    camion.cargar(bumblebee)
    paqueteDeLadrillos.cantidadDeLadrillos(300)
    camion.cargar(paqueteDeLadrillos)

    assert.equals(2400, camion.pesoTotal())
    assert.notThat(camion.estaExcedidoDePeso())

    paqueteDeLadrillos.cantidadDeLadrillos(400)

    assert.equals(2600, camion.pesoTotal())
    assert.that(camion.estaExcedidoDePeso())
  }

  test "Prueba de consultas de cargas por nivel de peligrosidad" {
    camion.cargar(bumblebee)
    bumblebee.estaEnModoRobot(false)
    camion.cargar(bateriaAntiaerea)
    bateriaAntiaerea.tieneMisiles(false)
    camion.cargar(residuosRadiactivos)

    assert.equals(bateriaAntiaerea, camion.cargaConNivelDePeligrosidad(0))
    assert.equals(bumblebee, camion.cargaConNivelDePeligrosidad(15))
    assert.equals(residuosRadiactivos, camion.cargaConNivelDePeligrosidad(200))
    assert.throwsException({camion.cargaConNivelDePeligrosidad(30)})
  }

  test "Prueba de consultas de cargas que superen cierta peligrosidad" {
    camion.cargar(knightRider)
    bateriaAntiaerea.tieneMisiles(true)
    camion.cargar(bateriaAntiaerea)

    assert.equals(#{bateriaAntiaerea}, camion.cargasConNivelPeligrosidadSuperiorA(15))
    assert.equals(#{bateriaAntiaerea}, camion.cargasMasPeligrosasQue(bumblebee))
    assert.equals(#{knightRider, bateriaAntiaerea}, camion.cargasConNivelPeligrosidadSuperiorA(2))
    assert.equals(#{knightRider, bateriaAntiaerea}, camion.cargasMasPeligrosasQue(paqueteDeLadrillos))
    assert.equals(#{}, camion.cargasConNivelPeligrosidadSuperiorA(200))
    assert.equals(#{}, camion.cargasMasPeligrosasQue(residuosRadiactivos))
  }

  test "Prueba de consultas por la habilitación del camión para circular en rutas" {
    bumblebee.estaEnModoRobot(true)
    camion.cargar(bumblebee)
    paqueteDeLadrillos.cantidadDeLadrillos(300)
    camion.cargar(paqueteDeLadrillos)

    assert.notThat(camion.puedeCircularEnRutaDeNivel(20))

    bumblebee.estaEnModoRobot(false)

    assert.that(camion.puedeCircularEnRutaDeNivel(20))

    paqueteDeLadrillos.cantidadDeLadrillos(400)

    assert.notThat(camion.puedeCircularEnRutaDeNivel(20))
  }
  
  test "Prueba de peso y peligrosidad de contenedor portuario y embalaje" {
    contenedorPortuario.cosasContenidas(#{})

    assert.equals(100, contenedorPortuario.peso())
    assert.equals(0, contenedorPortuario.nivelPeligrosidad())

    arenaAGranel.peso(50)
    contenedorPortuario.cosasContenidas(#{knightRider, arenaAGranel})
    assert.equals(650, contenedorPortuario.peso())
    assert.equals(10, contenedorPortuario.nivelPeligrosidad())

    embalajeDeSeguridad.cosaEmbalada(knightRider)

    assert.equals(500, embalajeDeSeguridad.peso())
    assert.equals(5, embalajeDeSeguridad.nivelPeligrosidad())

    bumblebee.estaEnModoRobot(false)
    embalajeDeSeguridad.cosaEmbalada(bumblebee)

    assert.equals(800, embalajeDeSeguridad.peso())
    assert.equals(7.5, embalajeDeSeguridad.nivelPeligrosidad())
  }

  test "Comprobacion de pesos de carga (¿Existen cargas con un peso correspondiente al rango indicado?)" {
    camion.cargar(bumblebee)
    paqueteDeLadrillos.cantidadDeLadrillos(300)
    camion.cargar(paqueteDeLadrillos)

    assert.that(camion.tieneCargaMasPesadaQue_YMasLigeraQue_(5, 700))
    assert.that(camion.tieneCargaMasPesadaQue_YMasLigeraQue_(5, 1000))
    assert.notThat(camion.tieneCargaMasPesadaQue_YMasLigeraQue_(900, 1000))
  }

  test "Determinando la carga más pesada" {
    assert.throwsException({ camion.cosaMasPesada() })
    
    camion.cargar(bumblebee)
    paqueteDeLadrillos.cantidadDeLadrillos(300)
    camion.cargar(paqueteDeLadrillos)

    assert.equals(bumblebee, camion.cosaMasPesada())
  }

  test "Prueba 1 de cálculo de bultos en el camión" {
    contenedorPortuario.cosasContenidas(#{knightRider, bumblebee})
    camion.cargar(contenedorPortuario)
    embalajeDeSeguridad.cosaEmbalada(residuosRadiactivos)
    camion.cargar(embalajeDeSeguridad)
    camion.cargar(arenaAGranel)
    paqueteDeLadrillos.cantidadDeLadrillos(50)
    camion.cargar(paqueteDeLadrillos)
    bateriaAntiaerea.tieneMisiles(false)
    camion.cargar(bateriaAntiaerea)

    assert.equals(9, camion.bultosTransportados())
  }

  test "Prueba 2 de cálculo de bultos en el camión" {
    paqueteDeLadrillos.cantidadDeLadrillos(150)
    camion.cargar(paqueteDeLadrillos)
    bateriaAntiaerea.tieneMisiles(true)
    camion.cargar(bateriaAntiaerea)
    contenedorPortuario.cosasContenidas(#{})
    camion.cargar(contenedorPortuario)

    assert.equals(5, camion.bultosTransportados())
  }

  test "Prueba 3 de cálculo de bultos en el camión" {
    paqueteDeLadrillos.cantidadDeLadrillos(350)
    camion.cargar(paqueteDeLadrillos)
    camion.cargar(arenaAGranel)

    assert.equals(4, camion.bultosTransportados())
  }

  test "Prueba 1 de simulación de accidente" {
    bumblebee.estaEnModoRobot(false)
    contenedorPortuario.cosasContenidas(#{knightRider, bumblebee})
    camion.cargar(contenedorPortuario)
    residuosRadiactivos.peso(30)
    embalajeDeSeguridad.cosaEmbalada(residuosRadiactivos)
    camion.cargar(embalajeDeSeguridad)
    arenaAGranel.peso(50)
    camion.cargar(arenaAGranel)
    paqueteDeLadrillos.cantidadDeLadrillos(100)
    camion.cargar(paqueteDeLadrillos)
    bateriaAntiaerea.tieneMisiles(true)
    camion.cargar(bateriaAntiaerea)

    camion.sufrirUnAccidente()

    assert.that(bumblebee.estaEnModoRobot())
    assert.equals(30, residuosRadiactivos.peso())
    assert.equals(70, arenaAGranel.peso())
    assert.equals(88, paqueteDeLadrillos.cantidadDeLadrillos())
    assert.notThat(bateriaAntiaerea.tieneMisiles())
  }

   test "Prueba 2 de simulación de accidente" {
    bumblebee.estaEnModoRobot(true)
    camion.cargar(bumblebee)
    residuosRadiactivos.peso(30)
    camion.cargar(residuosRadiactivos)
    paqueteDeLadrillos.cantidadDeLadrillos(10)
    camion.cargar(paqueteDeLadrillos)
    bateriaAntiaerea.tieneMisiles(false)
    camion.cargar(bateriaAntiaerea)

    camion.sufrirUnAccidente()

    assert.notThat(bumblebee.estaEnModoRobot())
    assert.equals(45, residuosRadiactivos.peso())
    assert.equals(0, paqueteDeLadrillos.cantidadDeLadrillos())
    assert.notThat(bateriaAntiaerea.tieneMisiles())
  }
}

describe "Pruebas de viajes al almacén" {

  method initialize() {
    almacen.almacenar(knightRider)
    almacen.almacenar(residuosRadiactivos)
    bumblebee.estaEnModoRobot(false)
    camion.cargar(bumblebee)
    arenaAGranel.peso(50)
    camion.cargar(arenaAGranel)
  }

  test "Caso: viaje por ruta 9 exitoso" {
    camion.transportar(almacen, ruta9)

    assert.equals(#{knightRider,bumblebee,residuosRadiactivos,arenaAGranel}, almacen.cosas())
    assert.equals(#{}, camion.cosas())
  }

  test "Caso: viaje por ruta 9 fallido por peligrosidad" {
    bumblebee.estaEnModoRobot(true)
    camion.transportar(almacen, ruta9)

    assert.equals(#{knightRider,residuosRadiactivos}, almacen.cosas())
    assert.equals(#{bumblebee,arenaAGranel}, camion.cosas())
  }

  test "Caso: viaje por ruta 9 fallido por exceso de peso" {
    arenaAGranel.peso(2000)
    camion.transportar(almacen, ruta9)

    assert.equals(#{knightRider,residuosRadiactivos}, almacen.cosas())
    assert.equals(#{bumblebee,arenaAGranel}, camion.cosas())
  }

  test "Caso: viaje por caminos vecinales exitoso" {
    caminosVecinales.pesoMaximoPermitido(2000)
    camion.transportar(almacen, caminosVecinales)

    assert.equals(#{knightRider,bumblebee,residuosRadiactivos,arenaAGranel}, almacen.cosas())
    assert.equals(#{}, camion.cosas())
  }

  test "Caso: viaje por caminos vecinales fallido" {
    caminosVecinales.pesoMaximoPermitido(300)
    camion.transportar(almacen, caminosVecinales)

    assert.equals(#{knightRider,residuosRadiactivos}, almacen.cosas())
    assert.equals(#{bumblebee,arenaAGranel}, camion.cosas())
  }
}